PROGRAM = "start." PROGRAM_CONTENT "end."

PROGRAM_CONTENT = (STATEMENT | DECLARATION) {(STATEMENT | DECLARATION)}

DECLARATION = (SIMPLE_DECLARATION | ARRAY_DECLARATION) "."
SIMPLE_DECLARATION = "define" IDENTIFIER {"," IDENTIFIER} "as" TYPE
ARRAY_DECLARATION = "define" IDENTIFIER "as array" "[" NATURAL_NUMBER "]" "of" TYPE
ARRAY_ELEMENT = IDENTIFIER "[" NATURAL_NUMBER "]"
TYPE = "number" | "text" 

STATEMENT = ASSIGN_STMT | IF_STMT | WHILE_STMT | INPUT_STMT | OUTPUT_STMT
INPUT_STMT = "read" (IDENTIFIER | ARRAY_ELEMENT) "."
OUTPUT_STMT  = "show" (EXPRESSION | TEXT) "."
ASSIGN_STMT = (IDENTIFIER | ARRAY_ELEMENT) "=" (EXPRESSION | TEXT) "."
EXPRESSION = EXPRESSION ("+" | "-") TERM | TERM
TERM = TERM ("*" | "/") FACTOR | FACTOR
FACTOR = "(" EXPRESSION ")" | NUMBER | IDENTIFIER | ARRAY_ELEMENT
IF_STMT = "condition" CONDITION "{" PROGRAM_CONTENT "}" ["otherwise" "{" PROGRAM_CONTENT "}"]
WHILE_STMT = "while" CONDITION "repeat" "{" PROGRAM_CONTENT "}"

CONDITION = "(" EXPRESSION COMPARATOR EXPRESSION ")"
COMPARATOR = "<" | "<=" | ">" | ">=" | "==" | "=/="